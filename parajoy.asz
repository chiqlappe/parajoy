
;-------------------------------
;ジョイポートから入力データを取得する
;IN	A=ビットパターン 調べたいビットを0にする
;	MSB[-,-,B1,B2,R,L,D,U]LSB
;OUT	CY=押下状態 0:押された
;-------------------------------
READ_JOY:
	OUT	($10),A
	IN	A,($40)
	RRA				;CY<-押下状態
	RET


;-------------------------------
;ポート#0入力 INP(0)
;IN	-
;OUT	A=%1R1L1D11
;-------------------------------
JOY0:
	IN	A,($08)			;"カナ"キーが押下されていたらジョイポートをスキャンする
	AND	%00100000
	JR	Z,.L1
	IN	A,($00)
	RET

.L1:	PUSH	DE
	LD	E,%11111111

	LD	A,%11111101		;DOWN "2"
	CALL	READ_JOY
	RR	E			;%D1111111
	RR	E			;%1D111111

	LD	A,%11111011		;LEFT "4"
	CALL	READ_JOY
	RR	E			;%L1D11111
	RR	E			;%1L1D1111

	LD	A,%11110111		;RIGHT "6"
	CALL	READ_JOY
	RR	E			;%R1L1D111
	RR	E			;%1R1L1D11

	LD	A,E

	POP	DE
	RET


;-------------------------------
;ポート#1入力 INP(1)
;IN	-
;OUT	A=%r111111U
;-------------------------------
JOY1:
	IN	A,($08)
	AND	%00100000
	JR	Z,.L1
	IN	A,($01)
	RET

.L1:	PUSH	DE
	LD	E,%11111111

	LD	A,%11101111		;B2 "RETURN"
	CALL	READ_JOY
	RR	E			;%r1111111
	RR	E			;%1r111111

	LD	A,%11111110		;UP "8"
	CALL	READ_JOY
	RL	E			;%r111111U

	LD	A,E

	POP	DE
	RET


;-------------------------------
;ポート#5入力 INP(5)
;IN	-
;OUT	A=%11111Z1X
;-------------------------------
JOY5:
	IN	A,($08)
	AND	%00100000
	JR	Z,.L1
	IN	A,($05)
	RET

.L1:	PUSH	DE
	LD	E,%11111111

	LD	A,%11101111		;B2 "Z"
	CALL	READ_JOY
	RL	E
	RL	E			;%111111Z1

	LD	A,%11011111		;B1 "X"
	CALL	READ_JOY
	RL	E			;%11111Z1X

	LD	A,E

	POP	DE
	RET


;-------------------------------
;ポート#9入力 INP(9)
;IN	-
;OUT	A=%1S111111
;-------------------------------
JOY9:
	IN	A,($08)
	AND	%00100000
	JR	Z,.L1
	IN	A,($09)
	RET

.L1:	LD	A,%11011111		;B1 "SPACE"
	CALL	READ_JOY

	LD	A,%11111111
	RET	C

	LD	A,%10111111
	RET

