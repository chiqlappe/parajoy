
;-------------------------------
;PC-8001 "SCRAMBLE" 用ジョイポートパッチ
;
;ゲーム本体のプログラムをロードしてからこのパッチを実行して下さい
;カナキーを押すとジョイポート入力が有効になります
;-------------------------------

START	equ	$9000
GAME	equ	$D55B		;SCRAMBLEの開始アドレス


	org	START

	LD	HL,P0
	CALL	PATCHER
	JP	GAME


;-------------------------------
;パッチ当て処理
;IN	HL=パッチデータ開始アドレス
;-------------------------------
PATCHER:
.L1:	LD	E,(HL)
	INC	HL
	LD	D,(HL)
	INC	HL

	LD	A,D
	OR	E
	RET	Z

	LD	C,(HL)
	INC	HL
	LD	B,0
	LDIR
	JR	.L1


;-------------------------------
;パッチデータ
; 対象アドレス
; パッチの長さ
; パッチ本体
;-------------------------------
P0:
	dw	$DBB6
	db	P4-$-1
	CALL	PATCH00
	NOP

P4:
	dw	$DBBB
	db	P5-$-1
	CALL	PATCH02
	NOP

P5:
	dw	$DCAC
	db	P6-$-1
	CALL	PATCH03
	NOP

P6:
	dw	$DDF1
	db	P7-$-1
	CALL	PATCH03
	NOP

P7:
	dw	$E6C1
	db	P8-$-1
	CALL	PATCH04
	NOP

P8:
	dw	$E6DC
	db	P_END-$-1
	CALL	PATCH05
	NOP

P_END:
	dw	$0000


;-------------------------------
;パッチ用サブルーチン
;-------------------------------

PATCH00:
	CALL	JOY0
	XOR	$FF
	RET


PATCH02:
	CALL	JOY1
	XOR	$FF
	RET


PATCH03:
	CALL	JOY5
	XOR	$FF
	RET


PATCH04:
	CALL	JOY5
	AND	$04
	RET


PATCH05:
	CALL	JOY5
	AND	$01
	RET



;-------------------------------
;ジョイポートから入力データを取得する
;IN	A=ビットパターン 調べたいビットを0にする
;	MSB[-,-,B1,B2,R,L,D,U]LSB
;OUT	CY=押下状態 0:押された
;-------------------------------
READ_JOY:
	OUT	($10),A
	IN	A,($40)
	RRA				;CY<-押下状態
	RET


;-------------------------------
;ポート#0入力 INP(0)
;IN	-
;OUT	A=%1R1L1D11
;-------------------------------
JOY0:
	IN	A,($08)			;"カナ"キーが押下されていたらジョイポートをスキャンする
	AND	%00100000
	JR	Z,.L1
	IN	A,($00)
	RET

.L1:	PUSH	DE
	LD	E,%11111111

	LD	A,%11111101		;DOWN "2"
	CALL	READ_JOY
	RR	E			;%D1111111
	RR	E			;%1D111111

	LD	A,%11111011		;LEFT "4"
	CALL	READ_JOY
	RR	E			;%L1D11111
	RR	E			;%1L1D1111

	LD	A,%11110111		;RIGHT "6"
	CALL	READ_JOY
	RR	E			;%R1L1D111
	RR	E			;%1R1L1D11

	LD	A,E

	POP	DE
	RET


;-------------------------------
;ポート#1入力 INP(1)
;IN	-
;OUT	A=%1111111U
;-------------------------------
JOY1:
	IN	A,($08)
	AND	%00100000
	JR	Z,.L1
	IN	A,($01)
	RET

.L1:	LD	A,%11111110		;UP "8"
	CALL	READ_JOY

	LD	A,%11111111
	RLA

	RET


;-------------------------------
;ポート#5入力 INP(5)
;IN	-
;OUT	A=%11111Z1X
;-------------------------------
JOY5:
	IN	A,($08)
	AND	%00100000
	JR	Z,.L1
	IN	A,($05)
	RET

.L1:	PUSH	DE
	LD	E,%11111111

	LD	A,%11101111		;B2 "Z"
	CALL	READ_JOY
	RL	E
	RL	E			;%111111Z1

	LD	A,%11011111		;B1 "X"
	CALL	READ_JOY
	RL	E			;%11111Z1X

	LD	A,E

	POP	DE
	RET


